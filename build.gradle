plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'eclipse'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.19.0'
    id 'com.adarshr.test-logger' version '3.2.0'
}

apply plugin:'com.diffplug.spotless'
spotless {
    format 'misc', {
        target '**/*.md', '**/*.properties', '**/.gitignore', '**/.openapi-generator-ignore', '**/*.yml', '**/*.xml', '**/**.json', '**/*.sql'
        targetExclude '**/build/**', '**/bin/**', '**/.settings/**', '**/.idea/**', '**/.gradle/**', '**/gradlew.bat', '**/licenses/**', '**/banner.txt', '.vscode/**'
        indentWithSpaces(4)
        endWithNewline()
        trimTrailingWhitespace()
    }

    groovyGradle {
        target '*.gradle', '**/*.gradle'
        targetExclude '**/build/**'
        greclipse()
        indentWithSpaces(4)
        endWithNewline()
        trimTrailingWhitespace()
    }

    lineEndings 'UNIX'
}

configure(this) {
    // NOTE: order matters!
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'checkstyle'
    configurations {
        implementation.setCanBeResolved(true)
        api.setCanBeResolved(true)
    }
    tasks.withType(JavaCompile) {
        options.compilerArgs += [
            "-Xlint:unchecked",
            "-Xlint:cast",
            "-Xlint:auxiliaryclass",
            "-Xlint:deprecation",
            "-Xlint:dep-ann",
            "-Xlint:divzero",
            "-Xlint:empty",
            "-Xlint:exports",
            "-Xlint:fallthrough",
            "-Xlint:finally",
            "-Xlint:module",
            "-Xlint:opens",
            "-Xlint:options",
            "-Xlint:overloads",
            "-Xlint:overrides",
            "-Xlint:path",
            "-Xlint:processing",
            "-Xlint:removal",
            "-Xlint:requires-automatic",
            "-Xlint:requires-transitive-automatic",
            "-Xlint:try",
            "-Xlint:varargs",
            "-Xlint:preview",
            "-Xlint:static",
            // -Werror needs to be disabled because EclipseLink's static weaving doesn't generate warning-free code
            // and during an IntelliJ recompilation, it fails
            //"-Werror",
            "-Xmaxwarns",
            1500,
            "-Xmaxerrs",
            1500
        ]
        options.deprecation = true
    }
    // Configuration for the spotless plugin
    // https://github.com/diffplug/spotless/tree/main/plugin-gradle
    spotless {
        java {
            targetExclude '**/build/**', '**/bin/**', '**/out/**'
            importOrder() //sort imports alphabetically
            removeUnusedImports()
            eclipse().configFile "$rootDir/config/formatter.xml"
            endWithNewline()
            trimTrailingWhitespace()
            // Enforce style modifier order
            custom 'Modifier ordering', {
                def modifierRanking = [
                    public      : 1,
                    protected   : 2,
                    private     : 3,
                    abstract    : 4,
                    default     : 5,
                    static      : 6,
                    final       : 7,
                    transient   : 8,
                    volatile    : 9,
                    synchronized: 10,
                    native      : 11,
                    strictfp    : 12]
                // Find any instance of multiple modifiers. Lead with a non-word character to avoid
                // accidental matching against for instance, "an alternative default value"
                it.replaceAll(/\W(?:public |protected |private |abstract |default |static |final |transient |volatile |synchronized |native |strictfp ){2,}/, {
                    // Do not replace the leading non-word character.  Identify the modifiers
                    it.replaceAll(/(?:public |protected |private |abstract |default |static |final |transient |volatile |synchronized |native |strictfp ){2,}/, {
                        // Sort the modifiers according to the ranking above
                        it.split().sort({ modifierRanking[it] }).join(' ') + ' '
                    }
                    )
                }
                )
            }
        }
        lineEndings 'UNIX'
    }
    // If we are running Gradle within Eclipse to enhance classes,
    // set the classes directory to point to Eclipse's default build directory
    if (project.hasProperty('env') && project.getProperty('env') == 'eclipse') {
        sourceSets.main.java.outputDir = file("$projectDir/bin/main")
    }
    // Configuration for the Checkstyle plugin
    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    dependencies {
        checkstyle 'com.puppycrawl.tools:checkstyle:10.3.1'
        checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.42.0'
    }
}

group = 'org.mifos'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2')
    }

    maven {
        url = uri('https://jfrog.sandbox.fynarfin.io/artifactory/fyn-libs-snapshot')
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.35.0"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.mifos:ph-ee-connector-common:1.8.1-SNAPSHOT'
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation 'com.google.code.gson:gson:2.9.0'
    testImplementation('io.rest-assured:rest-assured:5.1.1') {
        exclude group: 'org.codehaus.groovy'
    }

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
    implementation 'io.cucumber:cucumber-java:7.8.1'
    implementation 'io.cucumber:cucumber-spring:7.8.1'
    testImplementation 'io.cucumber:cucumber-junit:7.8.1'

    testRuntimeOnly("org.junit.vintage:junit-vintage-engine")
    checkstyle 'com.puppycrawl.tools:checkstyle:10.9.3'
    checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.44.1'
    implementation 'com.diffplug.gradle.spotless:spotless:2.4.1'
    implementation 'com.diffplug.spotless:spotless-plugin-gradle:6.17.0'
    implementation 'org.mifos:openapi-java-client:2.0.3-SNAPSHOT'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    implementation 'com.opencsv:opencsv:5.5.2'
    testImplementation 'org.apache.commons:commons-csv:1.5'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    implementation 'commons-validator:commons-validator:1.7'
}

tasks.named('test') {
    useJUnitPlatform()
}


configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
    testlogger {
        // pick a theme - mocha, standard, plain, mocha-parallel, standard-parallel or plain-parallel
        theme 'mocha'
        showSkipped false
        showStackTraces true
    }
}

test {
    systemProperty "cucumber.filter.tags", System.getProperty("cucumber.filter.tags")
    systemProperty "cucumber.filter.name", System.getProperty("cucumber.filter.name")
}
task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                '--plugin',
                'html:cucumber-report',
                '--plugin',
                'json:cucumber.json',
                '--plugin',
                'pretty',
                '--plugin',
                'html:build/cucumber-report.html',
                '--plugin',
                'json:build/cucumber-report.json',
                '--glue',
                'org.mifos.integrationtest.cucumber',
                'src/test/java/resources'
            ]
        }
    }
}
